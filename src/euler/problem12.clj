(ns euler.problem12
  "The sequence of triangle numbers is generated by adding the natural numbers. So the 7th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 = 28. The first ten terms would be:

  1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...

  Let us list the factors of the first seven triangle numbers:

   1: 1
   3: 1,3
   6: 1,2,3,6
  10: 1,2,5,10
  15: 1,3,5,15
  21: 1,3,7,21
  28: 1,2,4,7,14,28
  We can see that 28 is the first triangle number to have over five divisors.

  What is the value of the first triangle number to have over five hundred divisors?"
  (:require 
    [common.prime :refer [prime-factors]]
    [clojure.math.combinatorics :as combo]))

(def triangles
  "A lazy sequence of triangle numbers"
  (map first (iterate (fn [[x y]] [(+ x y) (inc y)]) [1 2])))

(defn solve []
  (->> triangles
    (map prime-factors)
    (map #(distinct (map (partial apply *) (combo/subsets %))))
    (map (fn [coll] [(count coll) (apply max coll)]))
    (drop-while #(> 500 (first %)))
    first
    second
    println))

(defn -main []
  (time (solve)))

